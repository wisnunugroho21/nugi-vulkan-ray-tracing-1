#version 460

// ------------- layout -------------

#define SHININESS 64
#define KEPSILON 0.00001

#include "core/struct.glsl"

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0, rgba8) uniform writeonly image2D targetImage;

layout(set = 0, binding = 1) uniform readonly RayTraceUbo {
  vec3 origin;
  vec3 background;
  uint numLights;
} ubo;

layout(set = 0, binding = 2) buffer readonly ObjectSsbo {
  Object objects[];
};

layout(set = 0, binding = 3) buffer readonly ObjectBvhSsbo {
  BvhNode objectBvhNodes[];
};

layout(set = 0, binding = 4) buffer readonly PrimitiveSsbo {
  Primitive primitives[];
};

layout(set = 0, binding = 5) buffer readonly PrimitiveBvhSsbo {
  BvhNode primitiveBvhNodes[];
};

layout(set = 0, binding = 6) buffer readonly VertexSsbo {
  Vertex vertices[];
};

layout(set = 0, binding = 7) buffer readonly LightSsbo {
  PointLight lights[];
};

layout(set = 0, binding = 8) buffer readonly LightBvhSsbo {
  BvhNode lightBvhNodes[];
};

layout(set = 0, binding = 9) buffer readonly TransformationSsbo {
  Transformation transformations[];
};

layout(set = 0, binding = 10, rgba32f) uniform readonly image2D positionResource;
layout(set = 0, binding = 11, rgba32f) uniform readonly image2D textCoordResource;
layout(set = 0, binding = 12, rgba32f) uniform readonly image2D albedoColorResource;
layout(set = 0, binding = 13, rgba32f) uniform readonly image2D materialResource;

layout(push_constant) uniform Push {
  uint randomSeed;
} push;

uvec2 imgSize = uvec2(imageSize(targetImage));

#include "core/random.glsl"
#include "core/trace.glsl"
#include "core/ggx.glsl"
#include "core/shape.glsl"
#include "core/material.glsl"
#include "core/render.glsl"

// ------------- Material -------------

ShadeRecord indirectGgxShade(Ray r, HitRecord hit, vec3 materialParams, vec3 albedoColor, uint additionalRandomSeed) {
  ShadeRecord scat;
  scat.nextRay.origin = hit.point;

  vec3 unitViewDirection = normalize(r.direction);
  float f0 = 0.16 * (materialParams.z * materialParams.z);

  vec3[3] globalOnb = buildOnb(reflect(unitViewDirection, hit.normal));
  scat.nextRay.direction = ggxGenerateRandom(globalOnb, materialParams.y, additionalRandomSeed);

  vec3 H = normalize(scat.nextRay.direction - r.direction); // half vector

  float NoL = max(dot(hit.normal, normalize(scat.nextRay.direction)), 0.001f);
  float NoV = max(dot(hit.normal, -1.0f * unitViewDirection), 0.001f);
  float NoH = max(dot(hit.normal, H), 0.001f);
  float VoH = max(dot(unitViewDirection, H), 0.001f);

  float brdf = ggxBrdfValue(NoV, NoL, NoH, VoH, f0, materialParams.y);

  scat.pdf = ggxPdfValue(NoH, NoL, materialParams.y);
  scat.radiance = partialIntegrand(albedoColor, brdf, NoL);
  
  return scat;
}

ShadeRecord indirectLambertShade(Ray r, HitRecord hit, vec3 materialParams, vec3 albedoColor, uint additionalRandomSeed) {
  ShadeRecord scat;
  scat.nextRay.origin = hit.point;

  uint triangleRand = randomUint(0, ubo.numLights, additionalRandomSeed);

  if (triangleRand < ubo.numLights) {
    scat.nextRay.direction = pointLightGenerateRandom(lights[triangleRand], hit.point);
  } else {
    scat.nextRay.direction = lambertGenerateRandom(buildOnb(hit.normal), additionalRandomSeed);
  }

  float NoL = max(dot(hit.normal, normalize(scat.nextRay.direction)), 0.001f);
  float brdf = lambertBrdfValue();

  scat.pdf = lambertPdfValue(NoL);
  scat.radiance = partialIntegrand(albedoColor, brdf, NoL); 
  
  return scat;
}

ShadeRecord directGgxShade(Ray r, HitRecord hit, vec3 materialParams, vec3 albedoColor, uint additionalRandomSeed) {
  ShadeRecord scat;
  Ray shadowRay;

  scat.radiance = vec3(0.0f);
  scat.pdf = 0.0f;

  shadowRay.origin = hit.point;
  uint lightIndex = randomUint(0, ubo.numLights - 1u, additionalRandomSeed);

  shadowRay.direction = pointLightGenerateRandom(lights[lightIndex], hit.point);
  HitRecord occludedHit = hitObjectBvh(shadowRay, 0.001f, 1.0f);

  if (!occludedHit.isHit) {
    vec3 unitLightDirection = normalize(shadowRay.direction);
    vec3 hittedPointLightFaceNormal = pointLightFaceNormal(lights[lightIndex], unitLightDirection, shadowRay.origin);

    float NloL = max(dot(hittedPointLightFaceNormal, -1.0f * unitLightDirection), 0.001f);
    float NoL = max(dot(hit.normal, unitLightDirection), 0.001f);    

    vec3 unitViewDirection = normalize(r.direction);
    vec3 H = normalize(shadowRay.direction - r.direction); // half vector

    float f0 = 0.16 * (materialParams.z * materialParams.z);
    
    float NoV = max(dot(hit.normal, -1.0f * unitViewDirection), 0.001f);
    float NoH = max(dot(hit.normal, H), 0.001f);
    float VoH = max(dot(unitViewDirection, H), 0.001f);

    float brdf = ggxBrdfValue(NoV, NoL, NoH, VoH, f0, materialParams.y);
    float sqrDistance = dot(shadowRay.direction, shadowRay.direction);
    float area = pointLightArea(lights[lightIndex]);

    scat.pdf = ggxPdfValue(NoH, NoL, materialParams.y);
    scat.radiance = partialIntegrand(albedoColor, brdf, NoL) * Gfactor(NloL, sqrDistance, area) * lights[lightIndex].color;
  }  

  return scat;
}

ShadeRecord directLambertShade(Ray r, HitRecord hit, vec3 materialParams, vec3 albedoColor, uint additionalRandomSeed) {
  ShadeRecord scat;
  Ray shadowRay;

  scat.radiance = vec3(0.0f);
  scat.pdf = 0.0f;

  shadowRay.origin = hit.point;
  uint lightIndex = randomUint(0, ubo.numLights - 1u, additionalRandomSeed);

  shadowRay.direction = pointLightGenerateRandom(lights[lightIndex], hit.point);
  HitRecord occludedHit = hitObjectBvh(shadowRay, 0.001f, 1.0f);

  if (!occludedHit.isHit) {
    vec3 unitLightDirection = normalize(shadowRay.direction);
    vec3 hittedPointLightFaceNormal = pointLightFaceNormal(lights[lightIndex], unitLightDirection, shadowRay.origin);

    float NloL = max(dot(hittedPointLightFaceNormal, -1.0f * unitLightDirection), 0.001f);
    float NoL = max(dot(hit.normal, unitLightDirection), 0.001f);    

    float sqrDistance = dot(shadowRay.direction, shadowRay.direction);
    float area = pointLightArea(lights[lightIndex]);
    float brdf = lambertBrdfValue();

    scat.pdf = lambertPdfValue(NoL);
    scat.radiance = partialIntegrand(albedoColor, brdf, NoL) * Gfactor(NloL, sqrDistance, area) * lights[lightIndex].color;
  }  

  return scat;
}

// ------------- Main -------------

void main() {
  uvec2 imgPosition = gl_GlobalInvocationID.xy;

  vec3 posWorld = imageLoad(positionResource, ivec2(imgPosition)).xyz;
  vec3 materialParams = imageLoad(materialResource, ivec2(imgPosition)).xyz;
  vec3 albedoColor = imageLoad(albedoColorResource, ivec2(imgPosition)).xyz;

  vec3 totalRadiance = vec3(0.0f);
  vec3 totalIndirect = vec3(1.0f);

  ShadeRecord indirectShadeResult, directShadeResult;
  Ray curRay;

  indirectShadeResult.pdf = 1.0f;
  curRay.origin = ubo.origin;
  curRay.direction = posWorld - ubo.origin;
  
  for(uint i = 0; i < 50; i++) {
    HitRecord hit = hitObjectBvh(curRay, 0.001f, 1000000.0f);
    if (!hit.isHit) {
      totalRadiance = totalRadiance + totalIndirect * ubo.background;
      break;
    }

    HitRecord hittedPointLight = hitLightBvh(curRay, 0.001f, hit.t);
    if (hittedPointLight.isHit) {
      if (i > 0u) {
        totalIndirect = totalIndirect * Gfactor(curRay, hittedPointLight);
      }

      totalRadiance = totalRadiance + totalIndirect * lights[hittedPointLight.hitIndex].color;
      break;
    }

    totalIndirect = totalIndirect / indirectShadeResult.pdf;
    // uint materialIndex = primitives[hit.hitIndex].materialIndex;

    if (materialParams.x >= randomFloat(i)) {
      indirectShadeResult = indirectGgxShade(curRay, hit, materialParams, albedoColor, i);
      directShadeResult = directGgxShade(curRay, hit, materialParams, albedoColor, i);
    } else {
      indirectShadeResult = indirectLambertShade(curRay, hit, materialParams, albedoColor, i);
      directShadeResult = directLambertShade(curRay, hit, materialParams, albedoColor, i);
    }

    float totalPdf = directShadeResult.pdf + indirectShadeResult.pdf;

    totalRadiance = totalRadiance + totalIndirect * directShadeResult.radiance * directShadeResult.pdf / totalPdf;
    totalIndirect = totalIndirect * indirectShadeResult.radiance * indirectShadeResult.pdf / totalPdf;

    curRay = indirectShadeResult.nextRay;
  }
  
  imageStore(targetImage, ivec2(imgPosition), vec4(totalRadiance, 1.0f));
}